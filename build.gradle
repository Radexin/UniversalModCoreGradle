plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

group = 'cam72cam.universalmodcore'
version = '0.1.4'
java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:4.4.1.201607150455-r'
}

java {
    withSourcesJar()
    withJavadocJar()
}

gradlePlugin {
    plugins {
        curseGradlePlugin {
            id = 'cam72cam.universalmodcore'
            implementationClass = 'cam72cam.universalmodcore.UMCPlugin'
            displayName = 'Universal Mod Core'
            description = 'Allow users to build the same code for any minecraft version'
            tags = ['maven', 'minecraft', 'forge']
        }
    }
    website = 'https://github.com/Radexin/UniversalModCoreGradle'
    vcsUrl = 'https://github.com/Radexin/UniversalModCoreGradle'
}

// Disable the pluginMaven publication
tasks.withType(PublishToMavenRepository) {
    onlyIf {
        publication.name == 'mavenJava'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = "https://mymavenrepo.com/repo/9606RfvdcWav0GQh8Ktw"
            credentials {
                username = "myMavenRepo"
                password = System.getenv("MAVENCI_PASS")
            }
        }
    }
}
